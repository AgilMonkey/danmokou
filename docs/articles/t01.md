# Tutorial 1: The First Bullet

## Part 1: One Bullet

Make sure to check out [the setup guide](setup.md) first.

Open up the BasicSceneOPENME scene and click on the mokou-boss object. In the inspector window, you should see a variable "Behavior Script" under "Boss BEH", with the text file "DMK Tutorial Setup 00". Go ahead and double click this to open it. Also go ahead and run the scene in Unity.

The script file should have the following contents:

```python
## Go to dnh.bagoum.com/docs/tutorials/00.html for the tutorial. 
pattern({ })
phase(0)
	action(block, 0)
		shift-phase-to(1)
		
## This is phase #1. 
<!> type(non, `Hello World`)
<!> hp(4000)
phase(0)
	action(block, 0)
		position(0, 1)
		
```

We will discuss most of the structure later. For now, let's get some stuff on screen.

Add the following line at the end under `position(0, 1)`:

```python
		sync(fireball-red/w, <0;0:2;0:0>, s(rvelocity(cxy(0, -2))))
```

**Click in the Unity game view and press R. The script will restart instantaneously and you should see one fireball bullet fired**. When following through tutorials, you should always copy code into your script and load it in the game view to see how it works.

**Note: If the bullet appears to be stuck in the center of the screen, press Esc to open the in-game pause menu and turn Legacy Renderer ON.**

How does this work? Let's take a closer look at the code.

- `sync`: This is a State Machine command that fires bullets synchronously (ie. instantaneously). It takes three arguments: a bullet style, an offset, and a patterning function.
  - `fireball-red/w`: This is a bullet style. `fireball` is a bullet type (see [the bullet reference](bullets.md) for a list), `red` is a color (see the bullet reference for a list), and `/w` is a colorization method. There are three colorization methods: `/w`, `/`, and `/b`. 
  - `<0;0:2;0:0>`: This is an offset. We'll come back to this later. For now, just read this as a vector2 `(2, 0)`.
  - `s`: This is the simplest patterning function, which fires a single simple bullet. It takes one argument: a path definition for the bullet's movement.
    - `rvelocity`: This is a path definition function that uses velocity.
      - `cxy(0, -2)`: This is the constant vector2 `(0, -2)`.

The way to read this line is as follows: "fire a fireball, in red/w color, starting at the position (1, 0), with velocity (0, -2)". The screen is 16 units wide and 9 units high. The playable area goes from X=[-5, 5] and Y=[-4.5, 4].

Try modifying the code to do the following: (solutions at the end)

1. Summon a green circle.

2. Make the bullet go to the right.

3. Make the bullet go up, but faster.



Next, let's take a closer look at the starting offset. Offsets are defined as V2RV2s, which are composed of five numbers:

- Nonrotational X offset (NX)
- Nonrotational Y offset (NY)
-  Rotational X offset (RX)
- Rotational Y offset (RY)
- Angle (A)

`<0;0:2;0:0>` has RX set to 2 and everything else set to zero.

Let's try changing the angle on the previous example. We'll set it to 90:

```python
		sync(fireball-red/w, <0;0:2;0:90>, s(rvelocity(cxy(0, -2))))
```

**Press R to reload the script.** Notice how the offset gets rotated by 90ยบ, so it is now upwards instead of rightwards, and the velocity also gets rotated by 90ยบ, so it is now rightwards instead of downwards.

We don't always want all of these effects when using the angle. Our first option is to use `nrvelocity` (Non-Rotational Velocity):

```python
		sync(fireball-red/w, <0;0:2;0:90>, s(nrvelocity(cxy(0, -2))))
```

With `nrvelocity`, the offset is rotated, but the velocity is not.

If we don't want the offset to rotate, then we can use the nonrotational X component instead of the rotational X component:

```python
		sync(fireball-red/w, <2;0:0;0:90>, s(rvelocity(cxy(0, -2))))
```

By using the nonrotational X component, the offset is not rotated, but the velocity is.

You may be wondering if you have to write out all five numbers every time you want to use a V2RV2. Luckily, you don't. There are a number of ways to simplify V2RV2 expressions:

- You can leave zeros blank. `<0;0:2;0:90>` = `<;:2;:90>`.
- If your NX and NY components are zero, then you can omit them altogether. `<0;0:2;0:90>` = `<2;0:90>` = `<2;:90>`
- If your NX, NY, RX, and RY components are zero, then you can omit them altogether. `<0;0:0;0:90>` = `<90>`
- If everything is zero, you can just write `<>`. 

If this makes sense, move on to part 2, where we work with multiple bullets. 

## Part 2: GSRepeat

Whenever we need to do something multiple times, we use a repeater. There are four repeaters, and they are all highly similar.

- GSRepeat (Generalized Synchronous Repeat) <xref:Danmaku.SyncPatterns>
- GCRepeat (Generalized Coroutine (Asynchronous) Repeat) <xref:Danmaku.AsyncPatterns>
- GIRepeat (Generalized IEnumerator (Asynchronous) Repeat) <xref:Danmaku.AsyncPatterns>
- GTRepeat (Generalized Task Repeat) <xref:SM.GTRepeat>

We'll deal with GSRepeat first.

Replace the code from the first part with this:

```python
		sync(arrow-red/w, <0;0:1;0:0>, gsrepeat({
				times(30)
				rv2incr(<10>)
			}, s(rvelocity(cxy(2, 0))))
		)
```

GSRepeat is a synchronous pattern function that takes synchronous pattern functions as children. In this case, we want to repeat the function `s`, so we put it inside a GSRepeat.

GSRepeat takes two arguments:

- an array of repeater modifiers,
- an array of children (or a single child). 

To write an array, we use the following format: (newlines/indenting optional)

```python
{
	item1
	item2
	item3
}
```

In this case, there are two modifiers: `times(30)` and `rv2incr(<10>)`.

`times` tells the engine how many times to run the repeater. Since we set it to 30, the engine will run the child 30 times, so 30 bullets will be summoned.

`rv2incr` tells the engine to increment the offset by a certain amount every iteration. Here, we are incrementing the offset by 10ยบ every iteration. 

Run the code in the engine and observe how the bullets are summoned. Then try to answer the following questions based on the previous section:

- What happens if you use `nrvelocity` instead of `rvelocity`?
- What happens if you use a nonrotational offset instead of a rotational offset?
- BONUS: What happens if you use a nonrotational offset AND a rotational offset, ie. `<2;0:1;0:0>`?

Let's quickly introduce a few modifiers:

- `circle` overrides `rv2incr` and instead summons all the bullets spread evenly around a circle.
- `spread(<RV2>)`  overrides `rv2incr` and instead summons all the bullets spread evenly within the range described.

Try answering the following questions:

4. How would we summon all the bullets evenly in a circle?
5. How would we summon all the bullets spread evenly across half a circle?
6. If you figured out #5, then your half circle probably pointed upwards. How could you make it point to the right instead? (Hint: go back to the starting offset.)
7. BONUS: If you figured out #6, then set the starting offset back to its original value and instead add the `center` modifier. Can you figure out what the center modifier does?

## Part 3: Nesting Repeaters

We can arbitrarily nest repeaters. First, run the following code:

```python
		sync(arrow-red/w, <0;0:1;0:0>, gsrepeat({
				times(3)
				rv2incr(<4>)
			}, s(rvelocity(cxy(2, 0))))
		)
```

Now, let's add another repeater:

```python
		sync(arrow-red/w, <0;0:1;0:0>, gsrepeat({
				times(10)
				rv2incr(<30>)
			}, gsrepeat({
				times(3)
				rv2incr(<4>)
			}, s(rvelocity(cxy(2, 0))))
		))
```

Now, each of the smaller groups are separated by 30ยบ.

Let's take a look at how color modification works in DMK. Let's say we want each group to have a single color that switches between red, blue, and green. We can use the color modifier:

```python
		sync(arrow-*/w, <0;0:1;0:0>, gsrepeat({
				times(10)
				rv2incr(<30>)
				color({ red blue green })
			}, gsrepeat({
				times(3)
				rv2incr(<4>)
			}, s(rvelocity(cxy(2, 0))))
		))
```

Note how we add a wildcard `*` in the starting bullet style. This indicates where the color modifier should make changes.

Somewhere above, I mentioned that there were three colorization methods. Let's go ahead and switch between them within the smaller group.

```python
	sync(arrow-**, <0;0:1;0:0>, gsrepeat({
			times(10)
			rv2incr(<30>)
			color({ red blue green })
		}, gsrepeat({
			times(3)
			rv2incr(<4>)
			color({ /w / /b })
		}, s(rvelocity(cxy(2, 0))))
	))
```
To make this work, all we need to do is use two wildcards instead of one. Wildcards will be resolved in order.

But what if we want to switch between colors in the other order, so each group has a single colorization method and multiple colors?

```python
	sync(arrow-*, <0;0:1;0:0>, gsrepeat({
			times(10)
			rv2incr(<30>)
			color({ */w */ */b })
		}, gsrepeat({
			times(3)
			rv2incr(<4>)
			color({ red blue green })
		}, s(rvelocity(cxy(2, 0))))
	))
```
The code above uses a single wildcard in the original style so the colorization method is placed at the end, and then adds another wildcard before the colorization method.

Now that we have a grasp of a few basic controls, let's look at the other repeater types.

## Part 4: GCRepeat

GCRepeat is an asynchronous pattern function that takes synchronous pattern functions as children. If you are doing simple fires, you will use GCRepeat often. 

Since GCRepeat is asynchronous, we need to use the `async` command. Here's the code from the previous example with GCRepeat instead:

```python
		async(arrow-red/w, <0;0:1;0:0>, gcrepeat({
				times(3)
				rv2incr(<4>)
			}, s(rvelocity(cxy(2, 0))))
		)
```

Run it, and you'll see that it does the same thing as GSRepeat!

The difference between GCRepeat and GSRepeat is that we get access to time-based modifiers. The most important such modifier is `wait`, which creates a delay between each iteration. 

```python
		async(arrow-red/w, <0;0:1;0:0>, gcrepeat({
				wait(30)
				times(3)
				rv2incr(<4>)
			}, s(rvelocity(cxy(2, 0))))
		)
```

(There are 120 frames per second and `wait` takes an argument in frames, so this waits 0.25 seconds between each iteration.)

Observe how there is a delay between each bullet spawn.

Let's give a sneak peek to one of the more powerful features of DMK. What if we wanted to change the amount of wait for every iteration? We could simply provide a function to the `wait` modifier:

```python
		async(arrow-red/w, <0;0:1;0:0>, gcrepeat({
				wait(* 10 t)
				times(10)
				rv2incr(<4>)
			}, s(rvelocity(cxy(2, 0))))
		)
```

Note: BDSM is a prefix-based language, so `10 * t` is written as `* 10 t`. 

When used inside a repeater modifier, `t` refers to the iteration number of the modifier. Therefore, this new modifier waits "ten frames times the iteration number of the repeater". 

In DMK, almost every input can be changed into a function. For example, let's make the increment based on the iteration number as well:

```python
		async(arrow-red/w, <0;0:1;0:0>, gcrepeat({
				wait(10)
				times(10)
				rv2incr(angle(* 3 t))
			}, s(rvelocity(cxy(2, 0))))
		)
```

Note: The `<NX;NY:RX;RY:A>` format for V2RV2 is a shorthand for constants only. If we want to use V2RV2 functions, we need to use the functions in @DMath.ExMRV2.

We can even use the iteration number to modify the bullet movement function. However, since the movement function is not a modifier, we have to use a slightly different method. A later tutorial will explain how this works, but observe how simple it is to do:

```python
	async(arrow-red/w, <0;0:1;0:0>, gcrepeat({
			wait(10)
			times(10)
			rv2incr(<4>)
			bindItr(loop)
		}, s(rvelocity(pxy(+ 1 * 0.4 &loop, 0))))
	)
```
Note: Use `pxy` if you require functions. `cxy` only accepts constants, but is more optimized internally.

## Part 5: GIRepeat

GIRepeat is an asynchronous pattern function that takes asynchronous pattern functions as children. This makes it almost identical to GCRepeat, but there is one critical modifier that GIRepeat uses often.

Let's start with some simple code.

```python
		async(ellipse-*/w, <1;:>, girepeat({
			wait(4)
			times(30)
			rv2incr(<10>)
		}, gcrepeat({
			wait(15)
			times(4)
			color({ pink blue green })
		} s(rvelocity(cx(2))))
		))
```

The GCRepeat spawns a pillar of bullets, and the GIRepeat repeats the GCRepeat. Note how the GIRepeat starts the second iteration before the first iteration's GCRepeat is done executing. If you want to wait for the previous execution to finish, then use the `wait-child` modifier:

```python
		async(ellipse-*/w, <1;:>, girepeat({
			wait(4)
			times(30)
			rv2incr(<10>)
			wait-child
		}, gcrepeat({
			wait(15)
			times(4)
			color	{ pink blue green }
		} s(rvelocity(cx(2))))
		))
```

The `wait-child` modifier is critical for timing asynchronous actions. Make sure you understand how it works when it's present or absent!

## Part 6: GTRepeat

GTRepeat is the final repeater. It is a State Machine (SM) that takes other SM as children. This means that almost all script commands can be nested under GTRepeat. 

Before we show GTRepeat, let's take a look at the `move` SM function, defined in <xref:SM.SMReflection>. Replace your firing code with this:

```python
move(2, nroffset(pxy(sine(2, 3, t), 0)))
```

The move function takes two arguments:

- Time, in seconds, of movement
- Path function

`nroffset` (Non-Rotational Offset) is a path function that uses offset instead of velocity to calculate position. In this case, we are using the `sine(period, amplitude, controller)` function to determine the x-offset of the boss. 

Note: In path functions, `t` is the lifetime of the moving object. In `move` functions, `t` is more specifically the amount of time that the move function has been active for.

What do you think will happen if you use `cosine` instead of `sine`? Go ahead and try it. 

Now that you understand the move function, let's use it with `gtrepeat`. 

```
		gtrepeat({
			wait(1s)
			times(2)
			wait-child
		}, move(2, nroffset(pxy(sine(2, 3, t), 0))))
```

Note: `1s` is the same as `120`. Since the wait modifier is in frames, this is equal to 1 second. See [the parsing reference](parsing.md) for an explanation of numeric suffixes.

The modifiers that GTRepeat uses are the same as GIRepeat. In this code, it will run the move function twice, waiting 1 second after each is finished.

Before we continue, try removing the `wait-child` property. What happens? 

It might appear that the move command gets interrupted by the second move command after 1 second. However, this is not actually the case. Overlapping move commands can give you strange results, and we will see some very strange results at the end of this section.

Now, let's make the boss fire some bullets while moving.

```python
		gtrepeat({
			wait(1s)
			times(2)
			wait-child
		}, { 
			move(2, nroffset(pxy(sine(2, 3, t), 0)))
			async(fireball-teal/, <>, gcrepeat({
				wait(20)
				times(_)
				for(2s)
			}, gsrepeat({
				times(20)
				circle
			}, s(rvelocity(cx(3))))))
		})
```

Note that we are now using an array of children for GTRepeat: a move command and an async firing command. The firing command should be fairly familiar, but we have introduced a `for` property. The `for` property limits the maximum number of frames allowed for the repeater. The repeater will stop if it iterates `times` times or if it takes more than `for` frames. In this case, we set `times` to infinity (`_` = infinity) and `for` to 2 seconds so it lines up with the movement function.

As a result, the boss will fire while moving.

What do you think will happen if we set `for` to `4s`? Go ahead and try it. 

Now, let's enhance this simple pattern with some cool modifiers. This part may be a bit difficult, and most of the cool stuff here will be explained in a later tutorial.

```python
		gtrepeat({
			wait(1s)
			times(2)
			wait-child
			bindItr(loop)
		}, { 
			move(2, nroffset(pxy(sine(2, 3, t), 0)))
			async(fireball-*/, <>, gcrepeat({
				wait(20)
				times(_)
				for(2s)
				colorf({ teal orange }, &loop)
			}, gsrepeat({
				times(20)
				circle
			}, s(polar(* 3 t, * pm1(&loop) * 30 t)))))
		})
```

First, we use `bindItr(loop)` to set the variable `&loop` to the iteration number (0 or 1, since there are two iterations) of the gtrepeat. We could give it another name, and if you are using multiple `bindItr`s you will need to give each of them a different name. 

Then, we use the `colorf(colors, indexer)` modifier, which selects a color based on the indexer function instead of the iteration number. In the first GTR iteration, &loop is 0, so it selects teal. In the second GTR iteration, &loop is 1, so it selects orange. (If the indexer is out of bounds, it will loop back into the array, so don't worry about out-of-bounds errors.)

What do you think would happen if you used just `color({ teal orange })`? Try it out.

The bullet function here is using the `polar` path function, which is similar to `roffset` (Rotational Offset), but is given in polar coordinates instead of cartesian coordinates. `polar` is the standard method of implementing "angular velocity" and similar concepts in DMK. The input here is:

- `radius = 3 * t`
- `theta = pm1(&loop) * 30 * t` (in degrees)

`pm1` or `pm1mod` (Plus-Minus 1 Mod) is a function that returns 1 if the input is even, and -1 if the input is odd. This means that it will return 1 during the first GTR loop and -1 during the second GTR loop, so the bullets rotate counterclockwise (positive direction) during the first loop and clockwise (negative direction) during the second loop. 

If you think you can make sense of this example, then try solving this problem:

8. What if we wanted the rotation to switch direction on every iteration of the GCRepeat instead of every iteration of the GTRepeat? How would we implement that?

Finally, let's get back to that strange result I promised at the beginning of this part. Using the code snippet above, remove the `wait-child` property and **reload the script by pressing R.**

Can you see what happens? While the boss is moving to the right, the teal bullets from the first iteration continue summoning on the left side! This is not a bug, but is one of the strange results that can occur when you overlap multiple movement functions on one entity. Try figuring out why this occurs.

That's all for this tutorial. 



Pro tip: parentheses and argument commas are actually optional in BDSM, with the exception of parser macros. In fact, I usually don't use them. They're in tutorials for familiarity, but you may find it more efficient to drop them. 

## Problem Solutions

1. Change `fireball-red/w` to `circle-green/w`
2. Change `cxy(0, -2)` to `cxy(2, 0)`
3. Change `cxy(0, -2)` to `cxy(0, 4)`
4. Change `rv2incr(<10>)` to `circle`
5. Change `rv2incr(<10>)` to `spread(<180>)`
6. Change `<0;0:1;0:0>` to `<0;0:1;0:-90>`
7. When using RV2Incr/Circle/Spread, it summons half the bullets on one side of the starting offset and half the bullets on the other side.
8. Add `bindItr(loop2)` to the GCRepeat modifiers, and change the pm1 to `pm1(&loop2)`.

