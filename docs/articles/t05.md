# Tutorial 5: Difficulty Controls

By default, the engine has the following difficulty levels: Easier, Easy, Normal, Hard, Lunatic, Ultra, Abex, and Assembly.

You can add or remove levels by modifying the <xref:Danmaku.Enums.DifficultySet> enum in Danmaku/Enums.cs, and also adjusting the values in the enum helpers in that file. 

Start with this code:

```python
<#> warnprefix
pattern { }
phase 0
	action block 0
		shift-phase-to 1
		

<!> type non  `Difficulty` #{
<!> root 0 1
phase 0
	action block 0
!!{ crpt 30
		async arrow-red/w <> gcrepeat {
			wait(120)
			times(_)
		} gsrepeat {
			times($crpt)
			circle
		} s tprot cx 2
```

This should all be familiar. 

In the earlier tutorials, I stressed using the R key to instantaneously reload scripts. There are in fact other script reload keys, and you may have pressed a few of them by accident. They are defined in Entities/Beh/LevelController.cs:ShouldRestart as follows: 

- 5: Reload on Easier difficulty
- T: Reload on Easy
- Y: Reload on Normal
- U: Reload on Hard
- I: (i) Reload on Lunatic
- O: Reload on Ultra
- P: Reload on Abex
- [: Reload on Assembly
- R: Reload on current difficulty

Try pressing some of these keys while the script is running. You should see the difficulty marker in the top right of the game UI change, although the pattern will stay the same.

Now, let's make some difficulty variation. Change the macro line to `!!{ crpt (dl * 30)`.

Reload the script in several difficulties. You'll see that the number of bullets changes every time. What's this magic number `dl`?

Internally, each difficulty has an associated *counter* and *value*. The counter is 0 on Easier, 1 on Easier, 4 on Lunatic, etc. The value is a multiplier which is roughly equal to `sqrt(2)^(counter-1)`, ie. each difficulty is approximately 40% greater than the previous difficulty. You don't need to remember this, and if you think these numbers are bad, you can change them in Danmaku/Enums.cs.

The function `dl` returns the ratio `CurrentDifficultyValue / LunaticDifficultyValue`. Effectively, we are using 30 as the reference value on Lunatic difficulty, and scaling it multiplicatively for all other difficulties.

We could also scale the waiting time using `dl`, although we should divide it, since higher difficulties have lower waiting times:

```python
		async arrow-red/w <> gcrepeat {
			wait(120 / dl)
			times(_)
		} gsrepeat {
			times(30)
			circle
		} s tprot cx 2
```

In addition to `dl`, there are also `dn`, `dh`, and `du` (guess what they do?), and you can add any that you need to <xref:DMath.ExMDifficulty>.

However, sometimes we want an additive modifier instead of a multiplicative one. In this case, we should use dc:

```python
		sync gdlaser-blue/b <> gsrepeat {
			times(6 + dc)
			circle
		} laser null 1 2 {
			dsfx
		}
```

This will spawn 6 lasers on Easier and 1 more for each difficulty above that.

There are also some convenience methods for grouping difficulties. For example, d3d2 returns -2 for Easier/Easy/Normal, 0 for Hard/Lunatic, and 2 for higher difficulties. You can always add new convenience functions to <xref:DMath.ExMDifficulty>.

```python
		async triangle-green/w <> gcrepeat {
			wait(8)
			times(10)
			target a Lplayer
		} gsrepeat {
			times(5 + d3d2)
			spread <80>
			center
		} s tprot cx 2
```

The last method for difficulty controls is using `select`, which is effectively an array indexing operator. I don't recommend this as it's kind of verbose and inelegant, and it requires manually setting values for all difficulties.

```python
!!{ crpt select dc { 10 15 20 25 30 40 50 60 }
		async arrow-red/w <> gcrepeat {
			wait(120)
			times(_)
		} gsrepeat {
			times($crpt)
			circle
		} s tprot cx 2
```

