pattern { }
<!> hide-timeout
phase 0
    action block 0.5
        vulnerable false
        @ n1,n2,nb,nt
            cull
        summon inode <> nb <> null null
        summon inode <> nt <> null null
        summon inode <> n1 <> null null
        summon inode <> n2 <> null null
        position 2 7
        shift-phase-to 7
        
##1 Dispulsion (cutin)
<!> spellname  `Starry Night: Dispulsion`
<!> hp 22000
<!> hpbar 1
phase 40 #{
    action block 0
        move 2 tpnrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 2.5
        summon cutin-mokou <> _ <> null file slide-rl
        wait-for 3
    action block 0
        vulnerable true
        position 0 2.5
        bullet x-laser-fire <> idelay 960 irepeat 960 _ <> cre 10 10 <> sfx
        bullet x-laser-on <> idelay 1320 irepeat 960 _ <> cre 10 10 <> sfx
        bullet laser24-yellow/w <-1;3:> idelay 960 irepeat 960 99 <> itargetx Lplayer cre 5 20 <0.1;:> slaser 0 -90
            null
            3 0.5
        bullet x-bubble <> irepeat
            clamp 130 200 linear 200 -5 x
             _ <> cre 24 5 <> sfx
        bullet x-fire-burst-1<> idelay 120 cre
            clamp 130 200 linear 200 -5 x
             _ <> sfx
        bullet circle-red/ws <2;:> irepeat
            clamp 130 200 linear 200 -5 x
             _ <>
            irandomanglet 0 360 irepeat 0 3 <;:120> itarget Lplayer csummonalongpolarf REL 0 2 60 12
            btf srstar 2 5 x
            btf ucossmooth 144 144 x
            s tp-rot phastendelta 0.016 lerpin 1 1.4 lerpfromto 2 3 cx 2 cx 1
    end
        clear bullet-fancy
        bullet-control ONCE circle-red/ws SOFTCULL cwheel-red/w _
#}

##2 Where Lines Meet (no cutin)
<!> spellname `Boundary: Where Lines Meet`
<!> hp 14000
<!> hpbar 0.5
phase 30 #{
    action block 0
        move 2 tpnrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 3
    action block 0
        vulnerable true
        position 0 3
        move 9999 tpnrot TPbTPtF 
            max
                vhometime 2 Lplayer
                vhome 1 Lplayer
            0
        bullet sun-red/w <;2:> idelay 240 cre 
        clamp 120 360 linear 360 -40 x
        _ <> targetx Lplayer ss
            btf linear 1 0.18 x
            tp-rot clampy -4 0 growbyadd 0 -2 zero
        bullet x-metal <> idelay 240 cre
        clamp 120 360 linear 360 -40 x
        _ <> sfx
        bullet x-fire-burst-1<> cre
            clamp 20 30 linear 32 -.1 x
            _ <> sfx
        @ wingL
            bullet arrow-red/w <> cre 
            clamp 20 30 linear 32 -.1 x
            _ <> s tprot growbyadd 0 -1.5 rotate
                sine 60 60 p
                randomrotate -20 20 cr 5 135
        @ wingR
            bullet arrow-orange/w <> cre
            clamp 20 30 linear 32 -.1 x
            _ <> s tprot growbyadd 0 -1.5 rotate
                sine 80 60 p
                randomrotate -20 20 cr 5 135
        bullet-control PERSIST arrow-red/w DY -10 > y 5
        bullet-control PERSIST arrow-red/w DX 13 < x -6.5
        bullet-control PERSIST arrow-orange/w DY -10 > y 5
        bullet-control PERSIST arrow-orange/w DX -13 > x 6.5
    end
        clear bullet-control
        clear bullet-fancy
        bullet-control ONCE arrow-orange/w SOFTCULL cwheel-orange/ _
        bullet-control ONCE arrow-red/w SOFTCULL cwheel-red/ _
        bullet-control ONCE sun-red/w SOFTCULL cwheel-red/ _
#}

##3 Ashen Appalachia (cutin)
<!> spellname  `Creation: Ashen Appalachia`
<!> hp 22000
<!> hpbar 1
phase 40 #{
    action block 0
        move 2 tpnrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 2.5
        summon cutin-mokou <> _ <> null file slide-rl
        wait-for 3
    action block 0
        vulnerable true
        position 0 2.5
        @ n1
            position -6.1 0
        @ n2
            position 6.1 0
        pool-control sc-* allowcull false
        bullet-control persist sc-guanyin cull > x 8
        bullet-control persist sc-smoke cull < x -8
        bullet x-fire-tech-8 <> cre
                clamp 240 480 linear 480 -30 x 
                _ <> sfx
        bullet x-fire-tech-6 <> idelay 120 cre
                clamp 300 480 linear 480 -25 x
                _ <> sfx
        @ n1
            bullet sc-guanyin <> cre
                clamp 240 480 linear 480 -30 x 
                _ <> randomy -4 4 repeatinner 2 <;:150> <;:60> repeat 30 <0.6;:> s tpnrot cx 2.5
        @ n2
            bullet sc-smoke <> idelay 120 cre 
                clamp 300 480 linear 480 -25 x
                _ <> randomy -4 4 repeatinner 2 <;:-30> <;:60> repeat 30 <0.6;:> s tpnrot cx -2
        bullet x-knock-grain <> cre 
            clamp 30 120 linear 120 -3 x
            _ <> sfx
        bullet lcircle-oblack/w <> cre 
            clamp 30 120 linear 120 -3 x
            _ <>
            randomx -2 2 randomy -1 1 s tp
                randomrotate 60 120 cx 2
                growbyadd 0 -1.2 cy 0.3
        repeat _ wait 
            move 1 tpnrot eased io-sine 1 stopsampling 0 TPbTP 
                vhometime 0.8 Lplayer
                zero
    end
        pool-control sc-* allowcull true
        clear bullet-control
        bullet-control ONCE sc-guanyin SOFTCULL cwheel-red/ _
        bullet-control ONCE sc-smoke SOFTCULL cwheel-pink/ _
        bullet-control ONCE lcircle-oblack/w SOFTCULL cwheel-oblack/w _
#}
        
##4 Coup de Vache (no cutin)
<!> spellname `Art of War: Coup de Vache`
<!> hp 17000
<!> hpbar 0.5
phase 30 #{
    action block 0
        move 2 tpnrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 -3
    action block 0
        vulnerable true
        position 0 -3
        bullet x-fire-burst-1<> cre 24 _ <> sfx
        bullet sc-smoke <> cre 12 _ <> repeatp defer 8 <0;0:45>
            s tp-rot rotate f-sine 420 700 p
                clampx 1.5 2 growbyadd -0.1 0 cx 2
        bullet sun-red/w <;-2:> idelay 120 cre 
        clamp 360 480 linear 480 -40 x
        _ <> targetx Lplayer ss
            btf linear 1.2 0.2 x
            tp-rot clampy 1.5 5 growbyadd 0 -2 cy 5
        bullet x-metal <> idelay 120 cre
        clamp 360 480 linear 480 -40 x
        _ <> sfx
    end
        clear bullet-control
        bullet-control ONCE * SOFTCULL cwheel-pink/ _
#}

##5 Atomic Seal (cutin)
<!> spellname `Atomic Seal: Heat Ray`
<!> hp 18000
<!> hpbar 1
phase 45 #{
    action block 0
        move 2 tpnrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 1.5
        summon cutin-mokou <> _ <> null file slide-rl
        wait-for 3
    action block 0
    ##Vulnerable in next block
        position 0 1.5
        anim attack
        bullet-control PERSIST circle-* FLIPX> 6 _
        bullet-control PERSIST circle-* FLIPX< -6 _
        bullet-control PERSIST circle-* FLIPY> 4.5 _
        bullet-control PERSIST circle-* FLIPY< -4.5 _
        bullet-control PERSIST circle-blue/w SFX x-bubble relcirc mokou <0;0;0.8>
        bullet-control PERSIST circle-blue/w TIME 100 relcirc mokou <0;0;0.8>
        bullet-control PERSIST circle-blue/w RESTYLE circle-red/w > t 100
        bullet-control PERSIST circle-red/w TIME 4 > t 105
        bullet-control PERSIST circle-red/w RESTYLE circle-blue/w < t 5
        bullet laser24-red/w <> iparent mokou c-once repeat 3 <0;0:120> mlaser 3
            polar
                btf 1.73205
                btf * -60 x
            tp-rot rotate * -60 &lt
				cr 1 150
            1 99
        bullet x-metal <> cre 30 8 <> sfx
        bullet circle-blue/w <0;3.5:0> cre 30 8 <0;-1:0> repeat 55 <> randomx -6 6 randomy -1 0 s tp-rot 
            switch 1.5 zero
                lerpfromtoback 100 101 104 105
                    randomrotate 0 360 cx 0.15
                    randomrotate -60 60 stopsampling 100.5 vhome 1.4 Lplayer
    action block 0
        vulnerable true
        move 9999 tpnrot max
            vhometime 5 Lplayer
            vhome 0.5 Lplayer
    end
        clear bullet-control
        clear bullet-fancy
        bullet-control ONCE * SOFTCULL cwheel-blue/ _
#}

##6 Fireworks (no cutin)
<!> spellname `Fireworks: Led by the Light`
<!> hp 26000
<!> hpbar 0.5
phase 35 #{
    action block 0
        move 2 tpnrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 1
    action block 0
        vulnerable true
        pos cxy 0 1
        bullet sc-guanyin <> cre 60 _ <> pssfx x-fire-burst-1randomx -3 -1 randomy 1 3 repeatinnerp defer 40 <0.3;:> <;:9> s tp-rot protate 7.416 cx 2
        bullet sc-smoke <> cre 90 _ <> pssfx x-fire-burst-1randomx 1 3 randomy 0 2 repeatinnerp defer 45 <0.3;:> <;:8> s tp-rot protate 7.416 cx 1.5
        bullet x-bubble <> idelay 600 cre 600 _ <> sfx
        bullet scircle-red/w <> idelay 0 cre 1200 _ <> s tp-rot
            stopsampling 0 vhome 6 TPtFbTP
                -6.5
                Lplayer
        bullet scircle-red/w <> idelay 600 cre 1200 _ <> s tp-rot
            stopsampling 0 vhome 6 TPtFbTP
                6.5
                Lplayer
        bullet-control-sm PERSIST scircle-red/w < x -6.5
            action block 0
                bullet laser24-red/w <;-.5:> cre 20 5 <;.2:> slaser 0 0
                    null
                    1.5 3
                bullet x-laser-fire <> cre 20 5 <> sfx
                _ 1.5
                    bullet x-laser-on <> cre 20 5 <> sfx
                break
        bullet-control-sm PERSIST scircle-red/w > x 6.5
            action block 0
                bullet laser24-red/w <> cre 80 5 <> targety Lplayer slaser 0 180
                    null
                    1 0.5
                bullet x-laser-fire <> cre 80 5 <> sfx
                _ 1
                    bullet x-laser-on <> cre 80 5 <> sfx
                break
        bullet-control PERSIST scircle-red/w CULL < x -6.5
        bullet-control PERSIST scircle-red/w CULL > x 6.5
    end
        clear bullet-control
        clear bullet-fancy
        bullet-control ONCE * SOFTCULL cwheel-red/ _
#}

##7 Wings Spread Wide (cutin)
<!> spellname `Extinguished: Wings Spread Wide`
<!> hp 33000
<!> hpbar 1
phase 47 #{
    action block 0
        @ nt
            position 0 5
        move 2 tpnrot eased out-sine 2 stopsampling 0 vhometime 2 constant 0 2
        summon cutin-mokou <> _ <> null file slide-rl
        wait-for 3
    action nb 0
        vulnerable true
        position 0 2
        bullet-control PERSIST strip-purple/b CULL relrect mokou <0;0.2:0.6;0.2:0>
!{
scf_f()
    clamp 48 64 linear 64 -0.2 x
!}
!{
scf(parent, style, incr, spd)
    @ %parent%
        bullet %style% <;:-110> crepeatfuncangle $scf_f _ 
            mod 10 linear 0 %incr% x
                repeat 6 <;:8> s tp-rot cx %spd%
!}
        bullet x-fire-burst-1<> cre $scf_f _ <> sfx
        $scf(wingL, sc-guanyin, 6.18, 1.8)
        $scf(wingR, sc-smoke, 3.82, 2.3)
    ~
        repeat-every 10 9999
            action block 0
                _ 7
                    bullet ellipse-red/w <> cre 90 4 <> repeat 31 <;:-6> s tp-rot cx 3
                bullet x-bubble <> idelay 840 cre 90 4 <> sfx
                _ 4.5
                    bullet x-incoming-2 <> conce sfx
                _ 4
                    @ nt
                        bullet strip-purple/b <0;0:0> cre 2 50 <> repeat 50 <> randomx -7 7 randomy 0 2 s tp-rot growbyadd 0 -3 cy -4
                bullet x-fire-tech-8 <> cre 24 5 <> sfx
!{
            wingf(parent, s)
                @ %parent%
                    bullet laser16-red/w <;:-90> conce repeat 30 <> rslaser 0 0 
                        +
                            bpf sine 51 %s%20 x
                            *
                                + 
                                    b 1.07
                                    bpf cosine 120 -1 x
                                btf clamp 0 %s%160 linear 0 %s%180  ^ x 2
                        null
                        1.5 linear 4 -0.04 x
!}
                $wingf(wingL, -)
                $wingf(wingR, +)
    repeat-every 0 9999
        action block 6
            move 4 tpnrot eased io-sine 4 stopsampling 0 vhometime 4 randomxy 3 4 1 3
        action block 6
            move 4 tpnrot eased io-sine 4 stopsampling 0 vhometime 4 randomxy -4 -3 1 3
    end
        clear bullet-control
        clear bullet-fancy
        bullet-control ONCE strip-purple/b SOFTCULL cwheel-purple/b _
        bullet-control ONCE sc-guanyin SOFTCULL cwheel-red/ _
        bullet-control ONCE sc-smoke SOFTCULL cwheel-pink/ _
#}

##8 Flight of the Phoenix (no cutin)
<!> spellname `Extinguished: Flight of the Phoenix`
<!> hp 30000
<!> hpbar 0.5
phase 45 #{
    end
        pool-control strip-purple/b reset
        clear bullet-fancy
        bullet-control ONCE strip-purple/b SOFTCULL cwheel-purple/b _
        bullet-control ONCE  lcircle-purple/ws SOFTCULL cwheel-purple/w _
    action block 0
        @ nt
            position 0 5
        move 2 tpnrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 0
    action block 0
        vulnerable true
        position 0 0
        @ nt
            bullet strip-purple/b <> cre 2 _ <> repeat 10 <> randomx -7 7 randomy 0 3 s tp-rot growbyadd 0 -0.7 cy -1
        @ nt
            bullet lcircle-purple/ws <> cre
                clamp 5 20 linear 20 -0.2 x
                _ <> randomx -7 7 randomy 0 3 s tp-rot growbyadd 0 -0.4 cy -1
        bullet-control PERSIST strip-purple/b CULL relrect mokou <0;0.2:3;0.2:0>
        _ 0.2
            repeat _ wait
                move 4 tpnrot switch 2
                    stopsampling 0 vhometime 2 randomxy -2 2 -0.5 0.5
                    zero
        bullet x-up-dual <> idelay 60 cre 480 _ <> sfx
        bullet x-fire-burst-1<> idelay 240 cre 480 _ <> sfx
        bullet x-laser-on <> idelay 460 cre 480 _ <> sfx
!{
wing(endpoint, base_angle, s)
        bullet laser24-red/ws <> iparent mokou conce emlaser 4 %endpoint%
            null
            tp-rot rotatelerprate 22
                cr 1 %base_angle%
                rotate
                    + %base_angle% swing 4 %s%70 &lt
                    cx 1
            1 999
!}
        $wing(tn1, 40, +)
        $wing(tn2, 140, -)
!{
feather(parent, sign)
        bullet laser16-red/ws <> iparent %parent% irepeat 480 _ <> conce repeat 12 <;:%sign%10> rslaser 0 %sign%50
            btf cosine 4 %sign%15 x
            null
            4 0.1
!}
        $feather(tn1, -)
        $feather(tn2, +)
#}

##9 Embers At Dusk (cutin)
<!> spellname `Boundary Sign: Embers At Dusk`
<!> hp 18000
<!> hpbar 1
phase 40 #{
    action block 0
        @ nb
            position 0 -5
        move 2 tpnrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 3.5
        summon cutin-mokou <> _ <> null file slide-rl
        wait-for 3
    action nb 0
        vulnerable true
        position 0 3.5
        @ nb
            bullet circle-blue/w <> cre 6 _ <> randomx -6 6 s tp-rot 
                randomtime 4 period 4
                        lerpfromtoback 0.5 1.5 2.5 3.5 cy 0.4 cy 0.7
    repeat-every 3 9999
        action block 0
            move 0.5 tpnrot eased out-sine 0.5 stopsampling 0.1 tpbtp 
                vhometime 0.5 Lplayer
                zero
        action block 0
            bullet x-metal <> conce sfx
            bullet sun-red/w <-4;:> conce rpt 3 <4;:> ss
                btf clamp 1.75 4 linear 3 -0.4 x
                tpnrot
                growbyadd 0 -0.3 cy -1
    end
        bullet-control ONCE sun-red/w SOFTCULL cwheel-red/ _
        bullet-control ONCE circle-blue/w SOFTCULL cwheel-blue/w _
#}

##Phoenix Butterfly (cutin)
<!> spellname `Explosive Sign: Phoenix Butterfly`
<!> hp 24000
<!> hpbar 1
phase 44 #{
    action block 0
        move 2 tpnrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 3
        summon cutin-mokou <> _ <> null file slide-rl
        wait-for 3
    action block 0
        vulnerable true
        position 0 3
        bpat mokou01oscar
    end
        bullet-control ONCE circle-red/w SOFTCULL cwheel-red/w _
        bullet-control ONCE lcircle-purple/w SOFTCULL cwheel-purple/ _
#}
        
##end card
<!> spellname `Thanks for playing` `(I didn't program anything for boss death yet)`
phase 0
	action block 0
		noop

///

## Unadded
#{
phase 30
# NONSPELL 1: BASIC CARDS
    action block 0
        position 0 2.5
    action block 0
        bullet x-incoming <> c-once sfx
        wait-for 1.3
    action block 0
        bullet x-fire-burst-1<> cre 60 4 <> sfx
        @ n1
            bullet sc-smoke <> cre 120 2 <;:6.18> repeat 36 <0;0:10> tp-rot lerpin 0 0.5 constant 3 0
        @ n2
            bullet sc-guanyin <> idelay 60 cre 120 2 <;:6.18> repeat 36 <0;0:10> tp-rot lerpin 0 0.5 constant 3 0
    action block 0.5
        bullet x-incoming <> c-once sfx
        move 1 tpnrot eased out-sine 1 stopsampling 0 vhometime 1 cy -3.5
        bullet circle-red/w <> conce rpt 2 <;:180> rpt 50 <> tp-rot
            protate 0.15 pgrowbyadd 0.1 0 constant 0.1 0
        bullet circle-red/w <> cre 4 30 <> rpt 2 <;:180> tp-rot lerpin 0 1 randomdir 2.3 -50 50
        @ n1
            position -3 -4
        @ n2
            position 3 -4
        wait-for 1
    action block 0
        bullet x-fire-burst-1<> cre 40 6 <> sfx
        @ n1
            bullet sc-smoke <> cre 80 3 <;:6.18> rpt 36 <;:10> tp-rot lerpin 0 1 constant 3 0
        @ n2
            bullet sc-guanyin <> idelay 40 cre 80 3 <;:6.18> rpt 36 <;:10> tp-rot lerpin 0 1 constant 3 0
    action block 0.5
        bullet x-incoming <> c-once sfx
        move 1 tpnrot eased out-sine 1 stopsampling 0 vhometime 1 cy 3.5
        bullet circle-red/w <> cre 4 30 <> rpt 2 <;:180> tp-rot lerpin 0 1 randomdir 2.3 -50 50
        @ n1
            position -6 4.5
        @ n2
            position 6 4.5
    action block 0
        anim attack
    action block 1
        bullet x-fire-burst-1<> cre 60 8 <> sfx
        bullet x-fire-burst-1<> idelay 480 cre 50 9 <> sfx
        bullet x-fire-burst-1<> idelay 960 cre 40 36 <> sfx
        @ n1,n2
            move 9999 period 8 switch 4
                constant 0 -2.25
                constant 0 2.25
        @ n1
            action block 0
                bullet sc-smoke <> cre 120 4 <;:6.18> rpt 36 <;:10> tp-rot lerpin 0 1 constant 3 0
                bullet sc-smoke <> idelay 480 cre 100 5 <;:3.82> rpt 36 <;:10> tp-rot lerpin 0 1 constant 3.2 0
                bullet sc-smoke <> idelay 960 cre 80 18 <> repeatd 36 <;:10> tp-rot
                    pgrowbyadd 0.05 0 lerpin 0 1 constant 3.5 0
                break
        @ n2
            action block 0
                bullet sc-guanyin <> idelay 60 cre 120 4 <;:6.18> rpt 36 <;:10> tp-rot lerpin 0 1 constant 3 0
                bullet sc-guanyin <> idelay 530 cre 100 5 <;:3.82> rpt 36 <;:10> tp-rot lerpin 0 1 constant 3.2 0
                bullet sc-guanyin <> idelay 990 cre 80 18 <> repeatd 36 <;:10> tp-rot
                    pgrowbyadd 0.05 0 lerpin 0 1 constant 3.5 0
                break
    end
        clear bullet
///
phase 40
    action block 0
        position 0 2.5
    action nb 0
        bullet arrow-pink/b <-6;2:0> irepeat 480 9999 <0;0:0> c-repeatevery 6 20 <0;-0.45:0> tp-rot
            lerpin 0 4
                randomrotate -40 40 constant 1 0
        bullet arrow-blue/b <-6;-7:0> idelay 120 irepeat 480 9999 <0;0:0> c-repeatevery 4 30 <0.4;0:0> tp-rot
            lerpin 0 3
                randomrotate -40 40 constant 0 1
        bullet arrow-teal/b <6;-7:0> idelay 240 irepeat 480 9999 <0;0:0> c-repeatevery 6 20 <0;0.45:0> tp-rot
            lerpin 0 2
                randomrotate -40 40 constant -1 0
        bullet arrow-red/b <6;2:0> idelay 360 irepeat 480 9999 <0;0:0> c-repeatevery 4 30 <-0.4;0:0> tp-rot
            lerpin 0 1
                randomrotate -40 40 constant 0 -1
    action nb 8
        anim attack
        _ 11.8
            anim attack
        bullet laser24-yellow/w <0;0:0> cre 720 9999 <0;0:13.75> repeat 10 <0;0:36> rslaser 0
            bpi sine 16 100 x
            pzero pzero 3 1
        bullet laser24-yellow/w <0;0:0> idelay 360 cre 720 9999 <0;0:22.25> repeat 10 <0;0:36> rslaser 0
            bpi sine 16 -100 x
            pzero pzero  3 1
        bullet laser24-purple/w <0;0:0> idelay 1440 cre 720 9999 <0;0:26.05> repeat 10 <0;0:36> rslaser 0
            bpi sine 16 -30 x
            pzero pzero 3 1
        bullet laser24-purple/w <0;0:0> idelay 1800 cre 720 9999 <0;0:9.95> repeat 10 <0;0:36> rslaser 0
            bpi sine 16 30 x
            pzero pzero 3 1
    end
        bullet-control ONCE arrow-* SOFTCULL cbutterfly-purple/ _
        clear bullet-fancy
#}
