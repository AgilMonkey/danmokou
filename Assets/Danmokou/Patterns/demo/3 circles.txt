pattern { }
phase 0 #{
    paction 0
        @  {nl nr nb nt }
            cull
        summon inode <> nb <> null null
        summon inode <> nt <> null null
        summon inode <> nl <> null null
        summon inode <> nr <x> null null
        shift-phase-to 8
#}
        
## 1. Non: Year End Blizzard [READY]
<!> type spell `Year End's Blizzard`
<!> hp 22000
<!> hpbar 0.5
phase 38 #{
    end 
        pool-control ellipse-blue/w reset
        bullet-control once ellipse-blue/w softcull cwheel-blue/w _
        bulletf-control once pather18-purple/b softcull cwheel-purple/b _
    paction 0
        move 2 tp-nrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 2
        ##wait-for 3
    action nb 0
        vulnerable true
        position 0 2
        @ nt
            position 0 5
        @ nt
            bullet ellipse-blue/w <> cre 5 _ <> repeat 4 <> randomx -8 8 randomy 0 2 s tpnrot pxy
                dsine(4, 0.15, + t brand 0 4)
                + -1.6 dsine(4.2, 0.2, + t brand 0 3.2)
    repeat-every 18 9
!{
blockf(s)
paction 0
    pool-control ellipse-blue/w reset
    move 3 tpnrot eased io-sine 3 stopsampling 0 vhometime 3 tpbtp cx %s%-5 superpose 0.3 loc 0.7 Lplayer
paction 0
    bullet pather18-purple/b <%s%-1.2;:> cre 40 18 <> randomy -4 4 pather 4 btf + 0.8 sine 4 0.3 t
        tpnrot lerpin 0 0.5 pxy %s%6
            sine(1, 0.3, + t * 1.618 p) 
    bullet trigger-down-2 <> cre 40 5 <> sfx
    wait-for 6
    timer restart fmult
    bullet-control PERSIST ellipse-blue/w FORCE relrect mokou <%s%5;:8;2:0>
        tpnrot pxy
            *   - 2 dist y [Lbeh mokou].y
                * %s%1.4  clamp(0, 1, fsoftmod(2.5, clamp(0, 99, - timersec(fmult) 1)))
            0
!}
        $blockf(+)
        $blockf(-)
        
#}
        
## 2. Christmas Flowers [READY]
<!> type spell `Christmas: A Winter of Blooming Flowers`
<!> hp 29000
<!> hpbar 1
phase 38 #{
    end
        ##clear bullet-fancy
        bullet-control ONCE arrow-teal/w SOFTCULL cwheel-teal/w _
        bullet-control ONCE arrow-red/w SOFTCULL cwheel-red/w _
        bullet-control ONCE arrow-green/w SOFTCULL cwheel-green/w _
        bulletf-control ONCE pather18-red/w SOFTCULL cwheel-pink/w _
        bulletf-control ONCE laser* CULL _
    paction 0
        move 2 tp-nrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 2
        summon cutin-mokou <> _ <> null file slide-rl
        wait-for 3
    paction 0
        vulnerable true
        position 0 2
        repeat _ wait 
                move 6 tpnrot switchH 4
                    zero
                    eased io-sine 2 stopsampling 0 vhometime 2 randomxy -3 3 1 3
!{
drawc(mult, rdi)
pxy
    * 0.1 * %mult% t
    * * 0.1 ^ * %mult% t 1.4
        bpf pm1mod %rdi%
!}
        bullet laser16-yellow/w <1;:> irepeat 720 _ <;:27.811> crepeatp defer 20 8 <;:45> pssfx x-laser-fire laser 0 null
            offset $drawc(2, p)
                zero
            3 0
        bullet _ <1;:> irepeat 720 _ <;:27.811> irepeatp defer 20 8 <;:45> csummonalongoffsetp add tangent 0 40 10 3
             $drawc(1, p1)
            repeatinnercolorp defer { arrow-teal/w arrow-red/w arrow-green/w } 9 <0.2;:> <;:40>
                s tp-rot bhastendeltaf 
                    bpf * 0.2 p2 
                    switch 2 zero
                        lerpin 0 1 growbyadd -0.1 0 cx -1
        bullet pather18-red/w <1;:> idelay 240 irepeat 720 _ <;:27.811> crepeatp defer 20 8 <;:45> pssfx x-laser-on pather 2 b 1
            ##22.5 = (3*120) / (40 - 0.2*120)
            offset  $drawc(22.5, x)
                zero
#}

## 3 Non: Sharpshooting Wind [READY]
<!> type spell `Sharpshooting Winter Wind`
<!> hp 18000
<!> hpbar 0.5
phase 30 #{
    end
        clear bullet-control
        bullet-control ONCE strip-purple/w SOFTCULL cwheel-teal/w _
        bulletf-control ONCE pather* SOFTCULL cwheel-purple/w _
    paction 0
        move 2 tp-nrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 3
    paction 0
        vulnerable true
        position 0 3
        bulletf-control-sm persist pather18-purple/w < 4 abs x
            paction 0
                bullet strip-purple/w <> cre 60 3 <;:12h> pssfx x-fire-tech-6 repeatp defer 30 <;:12> s tprot
                    switchH 1 cx 3
                        switchH 4.2 period 1 switchH 0.5
                                zero
                                cx 1
                            cx 2.2
                bullet pather18-blue/w <> conce repeat 4 <;:90> pather 4 3.3
                    tpnrot truerotatelerprate 70 
                        protate 90 cx 2 
                        stopsampling 1.5 vhome 2 Lplayer
                break
        bulletf-control persist pather18-purple/w restyle pather18-purple/b < 4 abs x
    repeat-every 7 9999
        paction 0
            bullet pather18-purple/w <> conce pssfx x-fire-tech-8 repeat 4 <> pather 1 1
                tpnrot eased in-sine 3 stopsampling 0 vhome 5 pxy
                    * 4 pm1mod p
                    * 3 pm1mod // p 2
        paction 0
            move 2 tpnrot eased io-sine 2 stopsampling 0 vhometime 2 randomxy -3 3 -1 3
#}

## 4 Cold Front [READY]
<!> type spell `Wintry Night: Uproar on the Cold Front`
<!> hp 22000
<!> hpbar 1
phase 36 #{
    end
        clear bullet-control
        bulletf-control once pather18-red/w softcull cwheel-red/w _
        bulletf-control once laser16r-blue/w softcull cwheel-blue/w _
        bullet-control once pointy-green/ softcull cwheel-green/ _
        bullet-control once pointy-yellow/ softcull cwheel-yellow/ _
    paction 0
        move 2 tp-nrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 3
        sync cutin-mokou <> summonup _ <> null file slide-rl
        wait-for 3
    action nb 0
        _ 2 
            vulnerable true
        position 0 3
        bulletf-control-sm persist pather18-red/w & > x 6 > y -4.5
            bullet pointy-green/ <> conce pssfx x-fire-burst-1repeat 36 <;:10> s tp-rot lerpfromtoback 0.5 1 2 2.5 cx 1.5 cx 0.5
        bulletf-control-sm persist pather18-red/w & < x -6 > y -4.5
            bullet pointy-yellow/ <> conce pssfx x-fire-burst-2 repeat 30 <;:12> s tp-rot lerpfromtoback 0.3 0.6 1.2 1.5 cx 2.5 cx 0
        bulletf-control persist pather18-red/w flipx> 6 _
        bulletf-control persist pather18-red/w flipx< -6 _
        bulletf-control persist laser16r-blue/w cull < y -8
        @ nt
            position -8 6
        @ nt
            bullet laser16r-blue/w <> cre
            clamp 180 240 linear 240 -8 x
            _ <> mlaser 0
                tpnrot tptf 0 - sine 3 0.5 t 1.2
                tpnrot pxy
                    1
                    *  + 0.7 sine 6 0.3 &lt
                    *  linear 0.2 0.1 fsoftmod 8 t
                        dsine 2 1 +(t, + *(2h, p) sine(4, 0.7, &lt))
                + 1.5 sine 3p 0.3 p
                2
    paction 0
        bullet pather18-red/w <1;:> cre 
            clamp 120 240 linear 240 -8 x
            _ <> pssfx x-fire-tech-8 modifybs
                * -2 (mod 2 p)
                0 0
            pather 4 2
            tprot lerpin 0 0.2 growbyadd 0 -0.2 growbyratioc -0.2 3 pperiod 2 protate 190 randomdir 7 -10 -2
#}

## 5 Non: Centripetal Release [READY]
<!> type spell `Rainstorm: Centripetal Release`
<!> hp 25000
<!> hpbar 0.5
phase 32 #{
    end
        poolf-control pather18-purple* allowcull true
        bulletf-control once pather18-purple* softcull cwheel-purple/ _
        bullet-control once ellipse-purple* softcull cwheel-blue/w _
        del@ pp1
        del@ pp2
    paction 0
        move 2 tp-nrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 3
    action nb 0
        vulnerable true
        position 0 3
        poolf-control pather18-purple* allowcull false
        bullet pather18-purple/w <1;:180> cre 30 15 <;:24> pssfx x-fire-tech-8 epather pp1 1 1
            tprot rotatewithtime 120 cx 4
        bullet pather18-purple/b <1;:> cre 30 15 <;:-24> epather pp2 1 1
            tprot rotatewithtime -120 cx 4
!{
pc(color)
bullet %color% <> idelay 2 cre
    clamp 14.53 16 linear 14.53 -0.02 x
    _ <> targetdir s tprot cx 1
!}
        ~@ pp1
            $pc(ellipse-purple/w)
        ~@ pp2
            $pc(ellipse-purple/b)
#}

## 6 City Roads Frosted Over [READY]
<!> type spell `Wintry Night: City Roads Frosted Over`
<!> hp 17000
<!> hpbar 1
phase 42 #{
    end
        bullet-control once circle-blue/w softcull cwheel-blue/w _
        bullet-control once shell-purple/b softcull cwheel-purple/b _
        bullet-control once shell-teal/b softcull cwheel-teal/b _
    paction 0
        move 1.5 tp-nrot eased out-sine 1.5 stopsampling 0 vhometime 1.5 cxy -5.5 4
        sync cutin-mokou <> summonup _ <> null file slide-rl
        wait-for 2.5
    paction 0
        vulnerable true
        position -5.5 5
!{
shellf(color)
bullet %color% <> cre 180 3 <;:20h> repeat 18 <;:20> repeat 18 <;:0.6> s tprot lerpfromto 0 1 cx 5 cx 0.7
!}
!{
btshellf()
paction 0
    @ nb
        $shellf(shell-purple/b)
    @ nt
        $shellf(shell-teal/b)
!}
!{
anime()
action nb 0
    bullet x-fire-burst-1<> idelay 1s cre 30 4 <> sfx
    bullet x-laser-on <> cre 30 4 <> sfx
    bullet x-incoming-2 <> idelay 9s conce sfx
    bullet circle-blue/w <;:> idelay 2 irepeat 2 60 <> itarget Lplayer icacheloc idelay 120 conce repeatp add 10 <> modifybs
        0 0  * 8 - 7 mod 15 p1
    s tp-rot pxy
        * bpf + 0.1 * 0.3 p2
           btf fswitchH 8 
                clamp 0 1 linear 1 -0.17 t
                clamp 0 -1 linear 0 -0.2 t
        0
saction 0
    move 0.25 tp-nrot eased out-sine 0.25 ss 0 vhometime 0.25 cxy -6.5 -4
    move 0.25 tp-nrot eased out-sine 0.25 ss 0 vhometime 0.25 cxy 5.5 -5
    move 0.25 tp-nrot eased out-sine 0.25 ss 0 vhometime 0.25 cxy 6.5 4
    move 0.25 tp-nrot eased out-sine 0.25 ss 0 vhometime 0.25 cxy -5.5 4
!}
    $anime()
    paction 0
        @ nb
            position -2 -5
        @ nt
            position 2 5
    $btshellf()
    paction 9
    $anime()
    paction 0
        @ nb
            position 4 -5
        @ nt
            position -4 5
    $btshellf()
    paction 9
    $anime()
    paction 0
        @ nb
            position 6 -2
        @ nt
            position -6 2
    $btshellf()
    paction 9
    $anime()
    
#}
   
## 7 Non: New Year's Gathering [READY]
<!> type spell `Forced Smile at a New Year's Gathering`
<!> hp 20000
<!> hpbar 1
phase 34 #{
    end
        clear bullet-control
        bullet-control once circle-purple/w softcull cwheel-blue/w _
        bullet-control once ellipse-purple/b softcull cwheel-blue/w _
        bullet-control once circle-teal/ softcull cwheel-teal/ _
    paction 0
        move 2 tp-nrot eased out-sine 2 stopsampling 0 vhometime 2 cxy -3 2
    paction 0
        vulnerable true
        position -3 2
    repeat-every 8 9999
        paction 0
            bullet x-fire-burst-1<> cre 32 17 <> sfx
            _ 6.5
                bullet x-incoming-2 <> conce sfx
            bullet circle-purple/w <> cre 16 30 <;:10.2> repeatp defer 36 <;:10> s tp-rot
                bhastendeltaf bpf * 16f p
                    lerpfromtoback 3.6 4.8 8.5 10
                        switch 5
                            cx 3.5 
                            rotate bpf * 6 clamp 0 14 p cx 2
                    zero
            bullet-control persist circle-purple/w restyle ellipse-purple/b < 8.5 + t * 0.2 p
            _ 3
                move 4 tpnrot eased io-sine 4 ss 0 vhometime 4 randomxy -3.2 3.2 1.9 2.2
            _ 3
                bullet circle-teal/ <> cre 20 25 <;:6.18> target Lplayer pssfx x-fire-burst-2 repeat 36 <;:10> s tp-rot lerpfromto 0 1 cx 5 cx 2.5
#}

## 8 New Sun [READY]
<!> type spell `Wintry Night: New Sun for a New Year`
<!> hp 60000
<!> hpbar 1
phase 72 #{
    end
        clear bullet-control
        pool-control sun-red/w allowcull true
        bulletf-control once pather18-green/w softcull cwheel-green/w _
        bulletf-control once pather18-red/w softcull cwheel-red/w _
        bulletf-control once laser16-blue/w cull _
        bullet-control once circle-black/ softcull cwheel-black/ _
        bullet-control once circle-purple/w softcull cwheel-purple/ _
        bullet-control once sun-red/w softcull cwheel-red/ _
    paction 0
        move 2 tp-nrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 6
        sync cutin-mokou <> summonup _ <> null file slide-rl
        pool-control sun-red/w allowcull false
        bulletf-control persist pather18-green/w flipX> 6 & < x 6.5 < y 2.5
        bulletf-control persist pather18-green/w flipX< -6 & > x -6.5 < y 2.5
        @ nt
            position 0 -9
        @ nt
            bullet sun-red/w <;-6:> conce pssfx x-metal ss 8
                offset zero switch 6 tptf 0 sine 24 6 t cy 6
        wait-for 6
    action nb 0
        vulnerable true
        @ nt       
            bullet pather18-red/w <8.2;:30> crepeatfuncangle 180 _
                    mod 9 * 9p t
                repeatp add 16 <;:9>
                    pather 4 1.5
                    tprot pxy
                        btf clamp 0 1 linear 0.4 0.2 t
                        btf * sine 3 0.5 t
                            cosine 30 1 clamp 0 6 t
        _ 6
            @ nt
                bullet circle-black/ <> cre
                    clamp 200 260 linear 260 -10 x
                _ <> pssfx x-boom randomx -4 4 randomy 5 6.2 repeatinner 45 <> <;:8> s tp-rot lerpin -0.2 0.7 cx 1.1
        _ 18
            @ nt
                bullet circle-purple/w <8.2;:> cre
                    clamp 120 140 linear 140 -2 x
                _ <;:8h> repeat 45 <;:8> s tp-rot lerpin 0 0.5 cx 0.6
    repeat-every 6 5
        paction 0
            position -8 0
            move 1 tpnrot eased out-sine 1 ss 0 vhometime 1 cxy -5 0
        paction 0
!{
keinef(color)
bullet x-keine <> cre 12 5 <> sfx
bullet %color% <;5:> cre 3 20 <;-.5:> s tprot 
    phastendelta -0.05 switchH 1
        vhome 0.01 Lplayer
        ss 0 vhome 5 Lplayer
!}
            $keinef(keine-yellow/w)
        paction 0
            move 1 tpnrot eased in-sine 1 ss 0 vhometime 1 cxy -8 0
        paction 0
            position 8 0
            move 1 tpnrot eased out-sine 1 ss 0 vhometime 1 cxy 5 0
        paction 0
            $keinef(keine-orange/w)
        paction 0
            move 1 tpnrot eased in-sine 1 ss 0 vhometime 1 cxy 8 0
            break
        break
    paction 0
        position 0 6
        move 1 tpnrot eased out-sine 1 ss 0 vhometime 1 cxy 0 3.5
    paction 0
        bullet x-laser-fire <> irepeat 4s 8 <> cre 8 9 <> sfx
        bullet x-laser-on <> idelay 220 irepeat 4s 8 <> cre 8 9 <> sfx
        bullet laser16-blue/w <> irepeat 4s 8 <> itarget Lplayer cre 8 9 <;:12> slaser 0 0 null 2 1
        bullet laser16-blue/w <> irepeat 4s 8 <> itarget Lplayer cre 8 9 <;:-12> slaser 0 0 null 2 1
        bullet x-fire-tech-6 <> idelay 9s irepeat 4s 6 <> cre 8 9 <> sfx
        bullet pather18-green/w <> idelay 8s irepeat 4s 6 <> itarget Lplayer idelay 1s cre 8 9 <;:12> pather 4 1.4 tprot cx 6
        bullet pather18-green/w <> idelay 8s irepeat 4s 6 <> itarget Lplayer idelay 1s cre 8 9 <;:-12> pather 4 1.4 tprot cx 6
        _ 20
            bullet sun-red/w <;12:> conce pssfx x-metal ss 8
                offset zero switch 12 tptf 0 sine 48 -7.5 t cy -7.5
        bullet x-laser-fire <> idelay 32s cre 8 45 <> sfx
        _ 32
            bullet laser16-blue/w <;:180> itarget Lplayer cre 1 360 <;:0.5> slaser 0 0 null 10 0
        _ 32
            bullet laser16-blue/w <;:180> itarget Lplayer cre 1 360 <;:-0.5> slaser 0 0 null 10 0
        
#}
   
///

## byakuren wip-1
<!> type spell `Starry Night: Piercing Rain`
<!> hp 25000
<!> hpbar 1
phase 28 #{
    end
        sfx phase-shift
    paction 0
        ##move 2 tp-nrot eased out-sine 2 stopsampling 0 vhometime 2 cxy 0 2
        ##summon cutin-mokou=cu slide-rl=START world:<> flip=
        ##wait-for 3
    paction 0
        vulnerable true
        position 0 3
        bullet pointy-blue/w <-1;0:> cre 12 _ <> repeatp add 2 <2;:> repeatinnerp defer 3 <;:-60> <;:60> s tprot brotatef 
            bpf * pm1mod p2 * 0.4 + -10 fsoftmod 20 p1
            cy -3
               
#}




///  
        bullet laser24-purple/b <1;:> conce repeat 2 <-2;:> rslaser 0 0
            +  bpf - * -30 pm1mod p 90
                * bpf pm1mod p btf clamp -320 0 linear 60 -130 t
            null
            1 10

///
    repeat-every 8 9999 
    paction 5
        move 3 tpnrot eased io-sine 3 ss 0 vhometime 3 randomxy -5 5 0 4
        bullet pather18-green/w <1;:> idelay 60 cre 120 3 <> pssfx x-fire-tech-8 pather 1 1
            tpnrot eased in-sine 4 ss 0 vhome 5 Lplayer
